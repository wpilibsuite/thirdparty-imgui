import java.nio.file.Files
import java.nio.file.Paths
import org.gradle.internal.os.OperatingSystem
import org.gradle.nativeplatform.toolchain.internal.msvcpp.VisualStudioLocator;
import org.gradle.nativeplatform.toolchain.VisualCpp;

plugins {
    id 'c'
    id 'cpp'
    id 'visual-studio'
    id 'edu.wpi.first.NativeUtils' version '2021.0.2'
}

if (OperatingSystem.current().isMacOsX()) {
    apply plugin: 'objective-c'
    apply plugin: 'objective-cpp'
}

repositories {
    mavenCentral()
}

ext.licenseFile = file("$rootDir/LICENSE.txt")

apply from: 'config.gradle'

def outputsFolder = file("$buildDir/allOutputs")

task copyAllOutputs(type: Copy) {
    destinationDir outputsFolder
}

build.dependsOn copyAllOutputs

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archivePath
    copyAllOutputs.from task.archivePath
}

nativeUtils.platformConfigs.named('osxx86-64') {
    it.objcCompiler.args.remove('-fobjc-weak')
    it.objcCompiler.args.remove('-fobjc-arc')
    it.objcppCompiler.args.remove('-fobjc-weak')
    it.objcppCompiler.args.remove('-fobjc-arc')
}

task gl3wGenerateTask(type:Exec) {
    def buildDirectory = Paths.get(projectDir.canonicalPath).resolve('build').resolve('gl3w')
    if (!Files.exists(buildDirectory)) {
        Files.createDirectories(buildDirectory)
    }
    workingDir buildDirectory.toString()
    if (OperatingSystem.current().isWindows()) {
        commandLine 'py', '../../gl3w/gl3w_gen.py'
    } else {
        commandLine 'python', '../../gl3w/gl3w_gen.py'
    }

}
model {
    binaries {
        all {
            it.tasks.withType(AbstractNativeSourceCompileTask) {
                dependsOn gl3wGenerateTask
            }
        }
    }
    components {
        imgui(NativeLibrarySpec) {
            sources {
                cpp {
                    source {
                        srcDirs 'imgui', 'fonts', 'image', 'implot', 'imgui/misc/cpp'
                        include '*.cpp', 'backends/imgui_impl_glfw.cpp', 'backends/imgui_impl_opengl3.cpp'
                    }
                    exportedHeaders {
                        srcDirs 'imgui', 'fonts', 'stb', 'implot', 'build/gl3w/include', 'glfw/include', 'imgui/misc/cpp'
                        include '*.h', 'backends/imgui_impl_glfw.h', 'backends/imgui_impl_opengl3.h'
                    }
                }
                c {
                    source {
                        srcDirs 'build/gl3w/src', 'glfw/src'
                        include 'gl3w.c', 'context.c', 'init.c', 'input.c', 'monitor.c', 'vulkan.c', 'window.c', 'egl_context.c', 'osmesa_context.c'
                    }
                    exportedHeaders {
                        srcDirs 'build/gl3w/include', 'glfw/include'
                        include '*.h'
                    }
                }
            }
            binaries.all {
                if (toolChain in VisualCpp) {
                    cppCompiler.args '-D_UNICODE', '-DUNICODE', '-DWIN32', '-D_WIN32', '-DSTRICT', '-DWIN32_LEAN_AND_MEAN', '-D_HAS_EXCEPTIONS=1'
                } else {
                    cppCompiler.args '-Wshadow', '-fexceptions', '-Wno-missing-field-initializers'
                }
                if (it in SharedLibraryBinarySpec) {
                    it.cCompiler.define '_GLFW_BUILD_DLL'
                    if (!it.targetPlatform.operatingSystem.isWindows()) {
                        it.linker.args << '-fvisibility=hidden'
                    }
                }
                if (it.targetPlatform.operatingSystem.isWindows()) {
                    it.cCompiler.define '_GLFW_WIN32'
                    linker.args << 'kernel32.lib' << 'Gdi32.lib' << 'User32.lib' << 'Shell32.lib' << 'd3d11.lib' << 'd3dcompiler.lib'
                    it.sources {
                        imguiWindowsCpp(CppSourceSet) {
                            source {
                                srcDirs 'imgui'
                                include 'backends/imgui_impl_dx11.cpp'
                            }
                            exportedHeaders {
                                srcDirs 'imgui'
                                include '*.h', 'backends/imgui_impl_dx11.h'
                            }
                        }
                        glfw3WindowsC(CSourceSet) {
                            source {
                                srcDirs 'glfw/src'
                                includes = [
                                    'win32_init.c',
                                    'win32_joystick.c',
                                    'win32_monitor.c',
                                    'win32_time.c',
                                    'win32_thread.c',
                                    'win32_window.c',
                                    'wgl_context.c',
                                ]
                            }
                        }
                    }
                } else if (it.targetPlatform.operatingSystem.isMacOsX()) {
                    it.cCompiler.args << '-fno-common'
                    it.cCompiler.define '_GLFW_COCOA'
                    it.objcCompiler.define '_GLFW_COCOA'
                    it.objcppCompiler.args << '-fobjc-arc'
                    it.linker.args << '-framework' << 'Metal' << '-framework' << 'MetalKit' << '-framework' << 'Cocoa' << '-framework' << 'IOKit' << '-framework' << 'CoreFoundation' << '-framework' << 'CoreVideo' << '-framework' << 'QuartzCore'
                    it.sources {
                        imguiMacObjectiveCpp(ObjectiveCppSourceSet) {
                            source {
                                srcDirs 'imgui'
                                include 'backends/imgui_impl_metal.mm'
                            }
                            exportedHeaders {
                                srcDirs 'imgui'
                                include '*.h', 'backends/imgui_impl_metal.h'
                            }
                        }
                        glfw3MacC(CSourceSet) {
                            source {
                                srcDirs 'glfw/src'
                                includes = [
                                    'cocoa_time.c',
                                    'posix_thread.c',
                                ]
                            }
                        }
                        glfw3MacObjC(ObjectiveCSourceSet) {
                            source {
                                srcDirs 'glfw/src'
                                includes = [
                                    'cocoa_init.m',
                                    'cocoa_joystick.m',
                                    'cocoa_monitor.m',
                                    'cocoa_window.m',
                                    'nsgl_context.m',
                                ]
                            }
                        }
                    }
                } else {
                    it.cCompiler.define '_GLFW_X11'
                    it.linker.args << '-lrt'
                    it.sources {
                        glfw3LinuxC(CSourceSet) {
                            source {
                                srcDirs 'glfw/src'
                                includes = [
                                    'x11_init.c',
                                    'x11_monitor.c',
                                    'x11_window.c',
                                    'xkb_unicode.c',
                                    'posix_time.c',
                                    'posix_thread.c',
                                    'glx_context.c',
                                    'linux_joystick.c',
                                ]
                            }
                        }
                    }
                }
            }
            binaries.withType(SharedLibraryBinarySpec) {
                buildable = false
            }
            appendDebugPathToBinaries(binaries)
        }
    }

}

apply from: 'publish.gradle'

wrapper {
    gradleVersion = '6.0.1'
}
