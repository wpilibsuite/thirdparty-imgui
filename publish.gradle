import org.gradle.nativeplatform.toolchain.internal.msvcpp.VisualStudioLocator;
import org.gradle.nativeplatform.toolchain.VisualCpp;

apply plugin: 'maven-publish'

publishing {
    repositories {
        maven {
            url "${System.getProperty('user.home')}/releases/maven/development"
        }
        maven {
            url "${System.getProperty('user.home')}/releases/maven/release"
        }
    }
}

def releaseNumber = 3

def pubVersion = "1.88-$releaseNumber"

def baseArtifactId = 'imgui'
def artifactGroupId = 'edu.wpi.first.thirdparty.frc2023'
def zipBaseName = '_GROUP_edu_wpi_first_thirdparty_frc2023_ID_imgui_CLS'

def outputsFolder = file("$project.buildDir/outputs")

def versionFile = file("$outputsFolder/version.txt")

task outputVersions() {
    description = 'Prints the versions of this to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(versionFile)

    doFirst {
        buildDir.mkdir()
        outputsFolder.mkdir()
    }

    doLast {
        versionFile.write pubVersion
    }
}

build.dependsOn outputVersions
copyAllOutputs.dependsOn outputVersions
copyAllOutputs.inputs.file versionFile
copyAllOutputs.from versionFile

task cppSourcesZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = zipBaseName
    classifier = "sources"

    from(licenseFile) {
        into '/'
    }

    from('imgui') {
        include '*.cpp'
        into '/imgui/'
    }

    from('imgui/backends') {
        include 'imgui_impl_glfw.cpp', 'imgui_impl_opengl3.cpp', 'imgui_impl_dx11.cpp', 'imgui_impl_metal.mm'
        into '/imgui/'
    }

    from('imgui/misc/cpp') {
        include '*.cpp'
        into '/imgui/'
    }

    from('implot') {
        include '*.cpp'
        into '/implot/'
    }

    from('fonts') {
        include '*.cpp'
        into '/imgui/'
    }

    from('build/gl3w/src') {
        include '*.c', '*.h'
        into '/gl3w/'
    }

    from('glfw/src') {
        include '*.c', '*.m', '*.h'
        into '/glfw/'
    }
}

task cppHeadersZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = zipBaseName
    classifier = "headers"

    from(licenseFile) {
        into '/'
    }

    from('imgui') {
        include '*.h'
        into '/'
    }

    from('imgui/backends') {
        include 'imgui_impl_glfw.h', 'imgui_impl_opengl3.h', 'imgui_impl_dx11.h', 'imgui_impl_metal.h'
        into '/'
    }

    from('imgui/misc/cpp') {
        include '*.h'
        into '/'
    }

    from('implot') {
        include '*.h'
        into '/'
    }

    from('fonts') {
        include '*.h'
        into '/'
    }

    from('build/gl3w/include') {
        into '/'
    }

    from('glfw/include') {
        into '/'
    }

    from('stb') {
        include 'stb_image.h'
        into '/'
    }
}

build.dependsOn cppHeadersZip
build.dependsOn cppSourcesZip

addTaskToCopyAllOutputs(cppHeadersZip)
addTaskToCopyAllOutputs(cppSourcesZip)

model {
    publishing {
        def gTaskList = createComponentZipTasks($.components, ['imgui'], zipBaseName, Zip, project, includeStandardZipFormat)

        publications {
            cpp(MavenPublication) {
                gTaskList.each {
                    artifact it
                }
                artifact cppHeadersZip
                artifact cppSourcesZip

                artifactId = baseArtifactId
                groupId artifactGroupId
                version pubVersion
            }
        }
    }
}
